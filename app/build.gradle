apply plugin: 'com.android.application'

android {
    def version = project.getProperties().get("version")
    if (version == null || "unspecified".equalsIgnoreCase(version)) {
        version = "TEST"
    }

    compileSdk 34
    defaultConfig {
        applicationId "tkaxv7s.xposed.sesame"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 31
        versionCode 80
        versionName version
    }
    ext {
        applicationName = "Sesame"
        applicationType = "Normal"
    }
    flavorDimensions.add("default")
    productFlavors {
        normal {
            dimension "default"
            ext.applicationType = "Normal"
        }
        compatible {
            dimension "default"
            ext.applicationType = "Compatible"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion '30.0.2'
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    implementation 'com.elvishew:xlog:1.11.0'

    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.17.1'

    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.5'

}
